<?xml version="1.0"?>
<!-- * Licensed to the Apache Software Foundation (ASF) under one * or more 
	contributor license agreements. See the NOTICE file * distributed with this 
	work for additional information * regarding copyright ownership. The ASF 
	licenses this file * to you under the Apache License, Version 2.0 (the * 
	"License"); you may not use this file except in compliance * with the License. 
	You may obtain a copy of the License at * * http://www.apache.org/licenses/LICENSE-2.0 
	* * Unless required by applicable law or agreed to in writing, * software 
	distributed under the License is distributed on an * "AS IS" BASIS, WITHOUT 
	WARRANTIES OR CONDITIONS OF ANY * KIND, either express or implied. See the 
	License for the * specific language governing permissions and limitations 
	* under the License. -->

<!-- * @author Felix Project Team mailto:dev@felix.apache.org -->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	version="1.0" xmlns:java="http://xml.apache.org/xslt/java"
	xmlns:redirect="org.apache.xalan.xslt.extensions.Redirect"
	extension-element-prefixes="redirect" exclude-result-prefixes="java">

	<!-- parameter declaration -->
	<xsl:param name="date" />
	<xsl:param name="author" />
	<xsl:param name="package" />
	<xsl:param name="descriptionfilename" />

	<xsl:template match="root">
		<project
			name="build for UPnP Device Source Generation generated by {$author} at {$date}"
			default="main" basedir=".">

			<target name="main" depends="init">
				<antcall target="generate" />
			</target>

			<target name="init">
				<mkdir dir="generated" />
				<tstamp>
					<format property="date" pattern="d-MMMM-yyyy hh:mm aa"
						locale="en" />
				</tstamp>
				<property name="generation.dir" value="../target/generated" />
				<property name="author" value="{$author}" />
			</target>

			<xsl:variable name="path" select="translate($package, '.', '/')" />

			<target name="generate">

				<mkdir dir="${{generation.dir}}/res" />
				<mkdir dir="${{generation.dir}}/src/{$path}" />
				<mkdir dir="${{generation.dir}}/src/{$path}/proxy" />

				<xslt in="{$descriptionfilename}" out="${{generation.dir}}/res/metadata.xml"
					style="device2metadata.xsl" classpath=".">
					<param name="date" expression="${{date}}" />
					<param name="author" expression="${{author}}" />
					<param name="package" expression="{$package}" />
					<outputproperty name="method" value="xml" />
					<outputproperty name="indent" value="yes" />
				</xslt>

				<xsl:apply-templates select="serviceList/service"
					mode="target" />

			</target>

			<!-- clean generated files and backup files -->
			<target name="clean">
				<delete dir="${{generation.dir}}" />
			</target>

		</project>

	</xsl:template>


	<xsl:template match="service" mode="target">

		<xsl:variable name="serviceTypeUPnP" select="./serviceType/text()" />
		<xsl:variable name="serviceIdUPnP" select="./serviceId/text()" />

		<xsl:variable name="serviceType"
			select="java:UPnPGenerationUtility.serviceType(./serviceType)" />
		<xsl:variable name="serviceTypeVersion"
			select="java:UPnPGenerationUtility.serviceTypeVersion(./serviceType)" />
		<xsl:variable name="classname" select="$serviceType" />
		<xsl:variable name="path" select="translate($package, '.', '/')" />

		<xslt in="res/service/{$serviceTypeVersion}.xml" out="${{generation.dir}}/src/{$path}/{$classname}.java"
			style="scdp2modelitf.xsl" classpath=".">
			<param name="date" expression="${{date}}" />
			<param name="author" expression="${{author}}" />
			<param name="package" expression="{$package}" />
			<param name="classname" expression="{$classname}" />
			<outputproperty name="method" value="text" />
			<outputproperty name="encoding" value="ISO-8859-1" />
			<outputproperty name="indent" value="yes" />
		</xslt>


		<xslt in="res/service/{$serviceTypeVersion}.xml"
			out="${{generation.dir}}/src/{$path}/proxy/{$classname}ProxyImpl.java"
			style="scdp2proxyimpl.xsl" classpath=".">
			<param name="date" expression="${{date}}" />
			<param name="author" expression="${{author}}" />
			<param name="package" expression="{$package}" />
			<param name="classname" expression="{$classname}" />
			<param name="serviceid" expression="{$serviceIdUPnP}" />
			<param name="servicetype" expression="{$serviceTypeUPnP}" />

			<outputproperty name="method" value="text" />
			<outputproperty name="encoding" value="ISO-8859-1" />
			<outputproperty name="indent" value="yes" />
		</xslt>
	</xsl:template>

</xsl:stylesheet>
